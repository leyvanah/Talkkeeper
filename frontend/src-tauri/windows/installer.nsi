Unicode true
ManifestDPIAware true
; Add in `dpiAwareness` `PerMonitorV2` to manifest for Windows 10 1607+ (note this should not affect lower versions since they should be able to ignore this and pick up `dpiAware` `true` set by `ManifestDPIAware true`)
; Currently undocumented on NSIS's website but is in the Docs folder of source tree, see
; https://github.com/kichik/nsis/blob/5fc0b87b819a9eec006df4967d08e522ddd651c9/Docs/src/attributes.but#L286-L300
; https://github.com/tauri-apps/tauri/pull/10106
ManifestDPIAwareness PerMonitorV2

!if "{{compression}}" == "none"
  SetCompress off
!else
  ; Set the compression algorithm. We default to LZMA.
  SetCompressor /SOLID "{{compression}}"
!endif

; Keep above !include to stay ahead of any plugin command
; see https://github.com/tauri-apps/tauri/pull/15422#discussion_r3289239624
{{#if signed_plugins_path}}
!addplugindir "{{signed_plugins_path}}"
{{/if}}

!include MUI2.nsh
!include FileFunc.nsh
!include x64.nsh
!include WordFunc.nsh
!include "utils.nsh"
!include "FileAssociation.nsh"
!include "Win\COM.nsh"
!include "Win\Propkey.nsh"
!include "StrFunc.nsh"
${StrCase}
${StrLoc}

!define MEETILY_PROGRESS_MESSAGE 0x84D1

; The frameless bootstrapper passes a unique token and polls this per-user key.
; The native NSIS progress control supplies continuous progress when available;
; these milestones keep status truthful even while a runtime installer owns the
; foreground work and the NSIS bar is temporarily stationary.
!macro MeetilyReportProgress Percent
  StrCpy $MeetilyReportedProgress ${Percent}
  ${If} $UpdateMode = 1
    SendMessage $HWNDPARENT ${MEETILY_PROGRESS_MESSAGE} ${Percent} 0
  ${EndIf}
  ${If} $MeetilyProgressToken != ""
    WriteRegDWORD HKCU "Software\meetily\InstallerProgress\$MeetilyProgressToken" "Percent" ${Percent}
  ${EndIf}
!macroend

{{#if installer_hooks}}
!include "{{installer_hooks}}"
{{/if}}

!define WEBVIEW2APPGUID "{F3017226-FE2A-4295-8BDF-00C3A9A7E4C5}"

!define MANUFACTURER "{{manufacturer}}"
!define PRODUCTNAME "{{product_name}}"
!define VERSION "{{version}}"
!define VERSIONWITHBUILD "{{version_with_build}}"
!define HOMEPAGE "{{homepage}}"
!define INSTALLMODE "{{install_mode}}"
!define LICENSE "{{license}}"
!define INSTALLERICON "{{installer_icon}}"
!define SIDEBARIMAGE "{{sidebar_image}}"
!define HEADERIMAGE "{{header_image}}"
!define UNINSTALLERICON "{{uninstaller_icon}}"
!define UNINSTALLERHEADERIMAGE "{{uninstaller_header_image}}"
!define MAINBINARYNAME "{{main_binary_name}}"
!define MAINBINARYSRCPATH "{{main_binary_path}}"
!define BUNDLEID "{{bundle_id}}"
!define COPYRIGHT "{{copyright}}"
!define OUTFILE "{{out_file}}"
!define ARCH "{{arch}}"
!define ADDITIONALPLUGINSPATH "{{additional_plugins_path}}"
!if /FileExists "${NSISDIR}\Plugins\x86-unicode\MeetilyProgress.dll"
  !define MEETILY_HAS_PROGRESS_PLUGIN
!endif
!define ALLOWDOWNGRADES "{{allow_downgrades}}"
!define DISPLAYLANGUAGESELECTOR "{{display_language_selector}}"
!define INSTALLWEBVIEW2MODE "{{install_webview2_mode}}"
!define WEBVIEW2INSTALLERARGS "{{webview2_installer_args}}"
!define WEBVIEW2BOOTSTRAPPERPATH "{{webview2_bootstrapper_path}}"
!define WEBVIEW2INSTALLERPATH "{{webview2_installer_path}}"
!define MINIMUMWEBVIEW2VERSION "{{minimum_webview2_version}}"
!define UNINSTKEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCTNAME}"
!define MANUKEY "Software\${MANUFACTURER}"
!define MANUPRODUCTKEY "${MANUKEY}\${PRODUCTNAME}"
!define UNINSTALLERSIGNCOMMAND "{{uninstaller_sign_cmd}}"
!define ESTIMATEDSIZE "{{estimated_size}}"
!define STARTMENUFOLDER "{{start_menu_folder}}"

Var PassiveMode
Var UpdateMode
Var NoShortcutMode
Var WixMode
Var OldMainBinaryName
Var MeetilyProgressToken
Var MeetilyReportedProgress
Var MeetilyInstallProgress
Var MeetilyDisplayedProgress

Name "${PRODUCTNAME}"
BrandingText "Talkkeeper"
OutFile "${OUTFILE}"

; We don't actually use this value as default install path,
; it's just for nsis to append the product name folder in the directory selector
; https://nsis.sourceforge.io/Reference/InstallDir
!define PLACEHOLDER_INSTALL_DIR "placeholder\${PRODUCTNAME}"
InstallDir "${PLACEHOLDER_INSTALL_DIR}"

VIProductVersion "${VERSIONWITHBUILD}"
VIAddVersionKey "ProductName" "${PRODUCTNAME}"
VIAddVersionKey "FileDescription" "${PRODUCTNAME}"
VIAddVersionKey "LegalCopyright" "${COPYRIGHT}"
VIAddVersionKey "FileVersion" "${VERSION}"
VIAddVersionKey "ProductVersion" "${VERSION}"

# additional plugins
!addplugindir "${ADDITIONALPLUGINSPATH}"

; Uninstaller signing command
!if "${UNINSTALLERSIGNCOMMAND}" != ""
  !uninstfinalize '${UNINSTALLERSIGNCOMMAND}'
!endif

; Handle install mode, `perUser`, `perMachine` or `both`
!if "${INSTALLMODE}" == "perMachine"
  RequestExecutionLevel admin
!endif

!if "${INSTALLMODE}" == "currentUser"
  RequestExecutionLevel user
!endif

!if "${INSTALLMODE}" == "both"
  !define MULTIUSER_MUI
  !define MULTIUSER_INSTALLMODE_INSTDIR "${PRODUCTNAME}"
  !define MULTIUSER_INSTALLMODE_COMMANDLINE
  !if "${ARCH}" == "x64"
    !define MULTIUSER_USE_PROGRAMFILES64
  !else if "${ARCH}" == "arm64"
    !define MULTIUSER_USE_PROGRAMFILES64
  !endif
  !define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY "${UNINSTKEY}"
  !define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME "CurrentUser"
  !define MULTIUSER_INSTALLMODEPAGE_SHOWUSERNAME
  !define MULTIUSER_INSTALLMODE_FUNCTION RestorePreviousInstallLocation
  !define MULTIUSER_EXECUTIONLEVEL Highest
  !include MultiUser.nsh
!endif

; Installer icon
!if "${INSTALLERICON}" != ""
  !define MUI_ICON "${INSTALLERICON}"
!endif

; Installer sidebar image
!if "${SIDEBARIMAGE}" != ""
  !define MUI_WELCOMEFINISHPAGE_BITMAP "${SIDEBARIMAGE}"
  !define MUI_WELCOMEFINISHPAGE_BITMAP_NOSTRETCH
!endif

; Enable header images for installer and uninstaller pages when either image is configured.
!if "${HEADERIMAGE}" != ""
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_BITMAP_NOSTRETCH
!else if "${UNINSTALLERHEADERIMAGE}" != ""
  !define MUI_HEADERIMAGE
!endif

; Installer header image
!if "${HEADERIMAGE}" != ""
  !define MUI_HEADERIMAGE_BITMAP "${HEADERIMAGE}"
!endif

; Uninstaller header image
!if "${UNINSTALLERHEADERIMAGE}" != ""
  !define MUI_HEADERIMAGE_UNBITMAP "${UNINSTALLERHEADERIMAGE}"
!endif

; Uninstaller icon
!if "${UNINSTALLERICON}" != ""
  !define MUI_UNICON "${UNINSTALLERICON}"
!endif

; ---------------------------------------------------------------------------
; Talkkeeper — full custom dark chrome (not stock Wizard97)
; Tokens: bg #0A0C10  surface #12151C  text #E6EBF5  muted #A8B3C7  teal #2DD4BF
; ---------------------------------------------------------------------------
!define MUI_BGCOLOR 0A0C10
!define MUI_TEXTCOLOR E6EBF5
!define MUI_INSTFILESPAGE_COLORS "E6EBF5 0A0C10"
!define MUI_LICENSEPAGE_BGCOLOR "0A0C10"

; Hide the grey header bitmap strip separators as much as MUI allows
!define MUI_HEADER_TRANSPARENT_TEXT

!define MUI_WELCOMEPAGE_TITLE "Welcome to Talkkeeper"
!define MUI_WELCOMEPAGE_TITLE_3LINES
!define MUI_WELCOMEPAGE_TEXT "Private meeting capture, transcription, and summaries — entirely on your PC.$\r$\n$\r$\nSetup will install Talkkeeper and local runtimes (WebView2, Visual C++, CUDA libraries).$\r$\n$\r$\nNothing is uploaded. Click Next to continue."

!define MUI_FINISHPAGE_TITLE "You're all set"
!define MUI_FINISHPAGE_TITLE_3LINES
!define MUI_FINISHPAGE_TEXT "Talkkeeper is installed.$\r$\n$\r$\nLaunch it to finish a 30-second onboarding (your name + a quick audio check). Everything stays on this machine."

!define MUI_INSTFILESPAGE_FINISHHEADER_TEXT "Install complete"
!define MUI_INSTFILESPAGE_FINISHHEADER_SUBTEXT "Talkkeeper is ready on this PC."
!define MUI_INSTFILESPAGE_ABORTHEADER_TEXT "Install cancelled"
!define MUI_INSTFILESPAGE_ABORTHEADER_SUBTEXT "No changes were finished."

!define MUI_CUSTOMFUNCTION_GUIINIT MeetilyGUIInit

; The in-app updater should emphasize status and overall progress, not expose
; NSIS's noisy extraction log as the primary interface.
ShowInstDetails hide
ShowUnInstDetails show
XPStyle on

; Progress bar messages (comctl32)
!define /ifndef PBM_SETBARCOLOR 0x409
!define /ifndef PBM_SETBKCOLOR  0x2001
!define /ifndef PBM_GETRANGE    0x407
!define /ifndef PBM_GETPOS      0x408

Var MeetilyDirDlg
Var MeetilyDirText
Var MeetilyDirBrowse
Var MeetilyDirHint
Var MeetilyFontTitle
Var MeetilyFontBody
Var MeetilyFinishLaunch
Var MeetilyFinishDesktop

Function MeetilyWelcomePageShow
  ${If} $PassiveMode = 1
    Abort
  ${EndIf}
  nsDialogs::Create 1018
  Pop $R0
  ${If} $R0 == error
    Abort
  ${EndIf}
  SetCtlColors $R0 E6EBF5 0A0C10

  ${NSD_CreateLabel} 0 2u 100% 12u "MEETILY  /  LOCAL AI MEETINGS"
  Pop $R1
  SetCtlColors $R1 50D5C7 0A0C10
  SendMessage $R1 ${WM_SETFONT} $MeetilyFontBody 1

  ${NSD_CreateLabel} 0 24u 100% 30u "Meetings stay yours."
  Pop $R1
  SetCtlColors $R1 F1F5F9 0A0C10
  SendMessage $R1 ${WM_SETFONT} $MeetilyFontTitle 1

  ${NSD_CreateLabel} 0 58u 100% 28u "Private recording, transcription, speaker labels, and summaries on your own PC."
  Pop $R1
  SetCtlColors $R1 A8B3C7 0A0C10

  ${NSD_CreateGroupBox} 0 96u 100% 62u "  ONE INSTALLER, THREE BACKENDS  "
  Pop $R1
  SetCtlColors $R1 7DD3FC 0A0C10
  ${NSD_CreateLabel} 14u 116u -14u 12u "NVIDIA CUDA    |    AMD / Intel / NVIDIA Vulkan    |    CPU fallback"
  Pop $R1
  SetCtlColors $R1 E2E8F0 0A0C10
  ${NSD_CreateLabel} 14u 136u -14u 12u "Setup detects compatible hardware automatically."
  Pop $R1
  SetCtlColors $R1 7F8CA3 0A0C10

  ${NSD_CreateLabel} 0 174u 100% 24u "No account. No subscription. No analytics."
  Pop $R1
  SetCtlColors $R1 94A3B8 0A0C10
  nsDialogs::Show
FunctionEnd

Function MeetilyWelcomePageLeave
FunctionEnd

; Windows 10/11 immersive dark title bar + dark control hints
Function MeetilyApplyOsDarkMode
  Push $R0
  ; DWMWA_USE_IMMERSIVE_DARK_MODE = 20 (Win10 20H1+); 19 on older 19H1 builds
  System::Call 'dwmapi::DwmSetWindowAttribute(p$HWNDPARENT, i20, *i 1, i4)'
  System::Call 'dwmapi::DwmSetWindowAttribute(p$HWNDPARENT, i19, *i 1, i4)'
  ; Undocumented uxtheme dark mode (ignore failures on older Windows)
  System::Call 'uxtheme::SetPreferredAppMode(i2)'
  System::Call 'uxtheme::AllowDarkModeForWindow(p$HWNDPARENT, i1)'
  System::Call 'uxtheme::FlushMenuThemes()'
  System::Call 'uxtheme::RefreshImmersiveColorPolicyState()'
  Pop $R0
FunctionEnd

Function MeetilyThemeHwnd
  ; $R9 = hwnd
  Exch $R9
  Push $R8
  SetCtlColors $R9 E6EBF5 0A0C10
  System::Call 'uxtheme::SetWindowTheme(p$R9, w"DarkMode_Explorer", p0)'
  Pop $R8
  Exch $R9
FunctionEnd

Function MeetilyGUIInit
  Call MeetilyApplyOsDarkMode
  SetCtlColors $HWNDPARENT E6EBF5 0A0C10
  ; Bottom branding strip + separator line
  Push $R0
  GetDlgItem $R0 $HWNDPARENT 1028
  IntCmp $R0 0 +3 0 0
    SetCtlColors $R0 5A6578 0A0C10
    SendMessage $R0 ${WM_SETTEXT} 0 "STR:Talkkeeper  ·  local AI meetings"
  GetDlgItem $R0 $HWNDPARENT 1035
  IntCmp $R0 0 +2 0 0
    ShowWindow $R0 ${SW_HIDE}
  GetDlgItem $R0 $HWNDPARENT 1036
  IntCmp $R0 0 +2 0 0
    ShowWindow $R0 ${SW_HIDE}
  GetDlgItem $R0 $HWNDPARENT 1039
  IntCmp $R0 0 +2 0 0
    ShowWindow $R0 ${SW_HIDE}
  GetDlgItem $R0 $HWNDPARENT 1040
  IntCmp $R0 0 +2 0 0
    ShowWindow $R0 ${SW_HIDE}
  ; Create brand fonts once
  CreateFont $MeetilyFontTitle "Segoe UI Semibold" 11 600
  CreateFont $MeetilyFontBody "Segoe UI" 9 400
  Pop $R0
FunctionEnd

; Paint every standard MUI control we can reach on the active page
Function MeetilyDarkenPage
  Call MeetilyApplyOsDarkMode
  SetCtlColors $HWNDPARENT E6EBF5 0A0C10
  Push $R0
  Push $R1
  Push $R2
  Push $R3

  FindWindow $R0 "#32770" "" $HWNDPARENT
  SetCtlColors $R0 E6EBF5 0A0C10
  System::Call 'uxtheme::SetWindowTheme(p$R0, w"DarkMode_Explorer", p0)'

  ; Keep stock header font metrics. Larger fonts in these fixed MUI rectangles
  ; overlap at common Windows display scaling values.
  GetDlgItem $R1 $HWNDPARENT 1037
  IntCmp $R1 0 +2 0 0
    SetCtlColors $R1 E6EBF5 0A0C10
  GetDlgItem $R1 $HWNDPARENT 1038
  IntCmp $R1 0 +2 0 0
    SetCtlColors $R1 A8B3C7 0A0C10

  ; Branding
  GetDlgItem $R1 $HWNDPARENT 1028
  IntCmp $R1 0 +2 0 0
    SetCtlColors $R1 5A6578 0A0C10

  ; Hide classic sunken header/footer lines
  GetDlgItem $R1 $HWNDPARENT 1035
  IntCmp $R1 0 +2 0 0
    ShowWindow $R1 ${SW_HIDE}
  GetDlgItem $R1 $HWNDPARENT 1036
  IntCmp $R1 0 +2 0 0
    ShowWindow $R1 ${SW_HIDE}
  GetDlgItem $R1 $HWNDPARENT 1039
  IntCmp $R1 0 +2 0 0
    ShowWindow $R1 ${SW_HIDE}
  GetDlgItem $R1 $HWNDPARENT 1040
  IntCmp $R1 0 +2 0 0
    ShowWindow $R1 ${SW_HIDE}

  ; Welcome / finish title + body (inner)
  GetDlgItem $R1 $R0 1201
  IntCmp $R1 0 +3 0 0
    SetCtlColors $R1 E6EBF5 0A0C10
    SendMessage $R1 ${WM_SETFONT} $MeetilyFontTitle 1
  GetDlgItem $R1 $R0 1202
  IntCmp $R1 0 +3 0 0
    SetCtlColors $R1 C5CDDC 0A0C10
    SendMessage $R1 ${WM_SETFONT} $MeetilyFontBody 1
  GetDlgItem $R1 $R0 1006
  IntCmp $R1 0 +2 0 0
    SetCtlColors $R1 C5CDDC 0A0C10
  GetDlgItem $R1 $R0 1023
  IntCmp $R1 0 +2 0 0
    SetCtlColors $R1 E6EBF5 0A0C10

  ; Directory / generic statics + edits + buttons inside inner dlg
  StrCpy $R2 0
  meetily_child_loop:
    FindWindow $R1 "" "" $R0 $R2
    StrCmp $R1 0 meetily_child_done
    StrCpy $R2 $R1
    SetCtlColors $R1 E6EBF5 0A0C10
    System::Call 'uxtheme::SetWindowTheme(p$R1, w"DarkMode_Explorer", p0)'
    Goto meetily_child_loop
  meetily_child_done:

  ; Nav buttons (outer)
  GetDlgItem $R1 $HWNDPARENT 1
  IntCmp $R1 0 +2 0 0
    System::Call 'uxtheme::SetWindowTheme(p$R1, w"DarkMode_Explorer", p0)'
  GetDlgItem $R1 $HWNDPARENT 2
  IntCmp $R1 0 +2 0 0
    System::Call 'uxtheme::SetWindowTheme(p$R1, w"DarkMode_Explorer", p0)'
  GetDlgItem $R1 $HWNDPARENT 3
  IntCmp $R1 0 +2 0 0
    System::Call 'uxtheme::SetWindowTheme(p$R1, w"DarkMode_Explorer", p0)'

  Pop $R3
  Pop $R2
  Pop $R1
  Pop $R0
FunctionEnd

Function MeetilyDarkenInstFiles
  Call MeetilyDarkenPage
  Push $R0
  Push $R1
  FindWindow $R0 "#32770" "" $HWNDPARENT

  ${If} $UpdateMode = 1
    SendMessage $HWNDPARENT ${WM_SETTEXT} 0 "STR:Talkkeeper Updater"
    !insertmacro MUI_HEADER_TEXT "Updating Talkkeeper" "Updating app files and refreshing local AI runtimes..."
    GetDlgItem $R1 $HWNDPARENT 1028
    SendMessage $R1 ${WM_SETTEXT} 0 "STR:Talkkeeper updater  ·  local AI meetings"
    GetDlgItem $R1 $HWNDPARENT 2
    SendMessage $R1 ${WM_SETTEXT} 0 "STR:Cancel update"
  ${Else}
    !insertmacro MUI_HEADER_TEXT "Installing Talkkeeper" "Copying app files and preparing local AI runtimes..."
  ${EndIf}

  ; Progress bar → teal on dark track
  GetDlgItem $R1 $R0 1004
  ${If} $R1 != 0
    StrCpy $MeetilyInstallProgress $R1
    ${If} $UpdateMode = 1
      ; Match the current bootstrapper's blue progress treatment.
      SendMessage $R1 ${PBM_SETBARCOLOR} 0 0xF7884B
    ${Else}
      SendMessage $R1 ${PBM_SETBARCOLOR} 0 0xBFD42D
    ${EndIf}
    SendMessage $R1 ${PBM_SETBKCOLOR} 0 0x1C1512
    System::Call 'uxtheme::SetWindowTheme(p$R1, w"", w"")'
  ${EndIf}

  ; Details log
  GetDlgItem $R1 $R0 1016
  IntCmp $R1 0 +4 0 0
    SetCtlColors $R1 A8B3C7 12151C
    System::Call 'uxtheme::SetWindowTheme(p$R1, w"DarkMode_Explorer", p0)'
    ShowWindow $R1 ${SW_HIDE}

  ; Status labels above progress
  GetDlgItem $R1 $R0 1006
  IntCmp $R1 0 +2 0 0
    SetCtlColors $R1 E6EBF5 0A0C10
  GetDlgItem $R1 $R0 1008
  IntCmp $R1 0 +2 0 0
    SetCtlColors $R1 A8B3C7 0A0C10
  GetDlgItem $R1 $R0 1027
  IntCmp $R1 0 +2 0 0
    ShowWindow $R1 ${SW_HIDE}

  ${If} $UpdateMode = 1
    ; The install thread blocks inside large File opcodes. This native UI-thread
    ; observer converts each current-file extraction percentage into byte-
    ; weighted overall progress and leaves status control 1006 unchanged.
    !ifdef MEETILY_HAS_PROGRESS_PLUGIN
      MeetilyProgress::Start
    !else
      StrCpy $MeetilyDisplayedProgress 0
      Call MeetilyRefreshInstallProgress
      nsDialogs::CreateTimer MeetilyRefreshInstallProgress 150
    !endif
  ${Else}
    StrCpy $MeetilyDisplayedProgress 0
    Call MeetilyRefreshInstallProgress
    nsDialogs::CreateTimer MeetilyRefreshInstallProgress 150
  ${EndIf}

  ; There is nowhere to navigate while files are being installed. Keep only
  ; Cancel visible, then reveal Next once .onInstSuccess runs.
  GetDlgItem $R1 $HWNDPARENT 1
  IntCmp $R1 0 +2 0 0
    ShowWindow $R1 ${SW_HIDE}
  GetDlgItem $R1 $HWNDPARENT 3
  IntCmp $R1 0 +2 0 0
    ShowWindow $R1 ${SW_HIDE}

  Pop $R1
  Pop $R0
FunctionEnd

Function MeetilyRefreshInstallProgress
  Push $R0
  Push $R1
  Push $R2
  Push $R3
  ${If} $MeetilyInstallProgress == ""
    Goto meetily_progress_done
  ${EndIf}
  !ifdef MEETILY_HAS_PROGRESS_PLUGIN
    ${If} $UpdateMode = 1
      Goto meetily_progress_done
    ${EndIf}
  !endif

  SendMessage $MeetilyInstallProgress ${PBM_GETRANGE} 1 0 $R1
  SendMessage $MeetilyInstallProgress ${PBM_GETRANGE} 0 0 $R2
  SendMessage $MeetilyInstallProgress ${PBM_GETPOS} 0 0 $R0
  ${If} $R2 > $R1
    IntOp $R0 $R0 - $R1
    IntOp $R0 $R0 * 100
    IntOp $R3 $R2 - $R1
    IntOp $R0 $R0 / $R3
  ${Else}
    StrCpy $R0 0
  ${EndIf}
  ${If} $R0 > 100
    StrCpy $R0 100
  ${ElseIf} $R0 < 0
    StrCpy $R0 0
  ${EndIf}
  ${If} $MeetilyReportedProgress > $R0
    StrCpy $R0 $MeetilyReportedProgress
  ${EndIf}
  ${If} $R0 > $MeetilyDisplayedProgress
    IntOp $R2 $R0 - $MeetilyDisplayedProgress
    ${If} $R2 > 6
      IntOp $MeetilyDisplayedProgress $MeetilyDisplayedProgress + 2
    ${Else}
      StrCpy $MeetilyDisplayedProgress $R0
    ${EndIf}
  ${EndIf}

  GetDlgItem $R1 $HWNDPARENT 1038
  ${If} $UpdateMode = 1
    SendMessage $R1 ${WM_SETTEXT} 0 "STR:Updating app files to version ${VERSION}  |  $MeetilyDisplayedProgress% complete"
  ${Else}
    SendMessage $R1 ${WM_SETTEXT} 0 "STR:Copying files and setting up local runtimes  |  $MeetilyDisplayedProgress% complete"
  ${EndIf}
  meetily_progress_done:
  Pop $R3
  Pop $R2
  Pop $R1
  Pop $R0
FunctionEnd

; ----- Custom install-location page (no stock groupbox wizard look) -----
Function MeetilyDirBrowseClick
  ${NSD_GetText} $MeetilyDirText $0
  nsDialogs::SelectFolderDialog "Choose Talkkeeper install folder" "$0"
  Pop $0
  ${If} $0 != "error"
  ${AndIf} $0 != ""
    ${NSD_SetText} $MeetilyDirText "$0"
  ${EndIf}
FunctionEnd

Function MeetilyDirPageShow
  ${If} $PassiveMode == 1
    Abort
  ${EndIf}

  !insertmacro MUI_HEADER_TEXT "Install location" "Pick a folder — the default is recommended"
  nsDialogs::Create 1018
  Pop $MeetilyDirDlg
  SetCtlColors $MeetilyDirDlg E6EBF5 0A0C10

  ${NSD_CreateLabel} 0 0 100% 28u "Talkkeeper will be installed for your Windows user account. You can change the folder below if you prefer."
  Pop $0
  SetCtlColors $0 A8B3C7 0A0C10
  SendMessage $0 ${WM_SETFONT} $MeetilyFontBody 1

  ${NSD_CreateLabel} 0 36u 100% 12u "FOLDER"
  Pop $0
  SetCtlColors $0 2DD4BF 0A0C10
  SendMessage $0 ${WM_SETFONT} $MeetilyFontTitle 1

  ${NSD_CreateText} 0 52u 76% 14u "$INSTDIR"
  Pop $MeetilyDirText
  SetCtlColors $MeetilyDirText E6EBF5 12151C
  System::Call 'uxtheme::SetWindowTheme(p$MeetilyDirText, w"DarkMode_Explorer", p0)'

  ${NSD_CreateButton} 78% 51u 22% 15u "Browse…"
  Pop $MeetilyDirBrowse
  ${NSD_OnClick} $MeetilyDirBrowse MeetilyDirBrowseClick
  System::Call 'uxtheme::SetWindowTheme(p$MeetilyDirBrowse, w"DarkMode_Explorer", p0)'

  ${NSD_CreateLabel} 0 78u 100% 24u "Approx. 810 MB required for the app, models bundle hooks, and GPU runtimes. WebView2 may add a little more on first run."
  Pop $MeetilyDirHint
  SetCtlColors $MeetilyDirHint 5A6578 0A0C10

  ${NSD_CreateLabel} 0 110u 100% 36u "After files copy, setup quietly ensures WebView2, Visual C++, and CUDA libraries. An NVIDIA GPU is used automatically when a driver is present."
  Pop $0
  SetCtlColors $0 A8B3C7 0A0C10

  Call MeetilyDarkenPage
  nsDialogs::Show
FunctionEnd

Function MeetilyDirPageLeave
  ${NSD_GetText} $MeetilyDirText $INSTDIR
  ${If} $INSTDIR == ""
    MessageBox MB_ICONEXCLAMATION "Choose an install folder."
    Abort
  ${EndIf}
  ; Create the directory now so a bad path fails early
  CreateDirectory "$INSTDIR"
  ${IfNot} ${FileExists} "$INSTDIR"
    MessageBox MB_ICONEXCLAMATION "Could not create:$\r$\n$INSTDIR"
    Abort
  ${EndIf}
FunctionEnd

Function MeetilyFinishPageShow
  ${If} $PassiveMode = 1
    Abort
  ${EndIf}
  nsDialogs::Create 1018
  Pop $R0
  ${If} $R0 == error
    Abort
  ${EndIf}
  SetCtlColors $R0 E6EBF5 0A0C10

  ${NSD_CreateLabel} 0 6u 100% 14u "INSTALL COMPLETE"
  Pop $R1
  SetCtlColors $R1 50D5C7 0A0C10

  ${NSD_CreateLabel} 0 32u 100% 32u "Talkkeeper is ready."
  Pop $R1
  SetCtlColors $R1 F1F5F9 0A0C10
  SendMessage $R1 ${WM_SETFONT} $MeetilyFontTitle 1

  ${NSD_CreateLabel} 0 72u 100% 38u "Your selected transcription backend and local runtimes are installed. First launch includes a short name and audio setup."
  Pop $R1
  SetCtlColors $R1 A8B3C7 0A0C10

  ${NSD_CreateCheckbox} 8u 128u -8u 14u "Launch Talkkeeper now"
  Pop $MeetilyFinishLaunch
  SetCtlColors $MeetilyFinishLaunch E6EBF5 0A0C10
  ${NSD_Check} $MeetilyFinishLaunch

  ${NSD_CreateCheckbox} 8u 154u -8u 14u "Create a desktop shortcut"
  Pop $MeetilyFinishDesktop
  SetCtlColors $MeetilyFinishDesktop E6EBF5 0A0C10

  ${NSD_CreateLabel} 0 188u 100% 20u "All app data remains local unless you explicitly configure a cloud provider."
  Pop $R1
  SetCtlColors $R1 64748B 0A0C10

  GetDlgItem $R1 $HWNDPARENT 1
  SendMessage $R1 ${WM_SETTEXT} 0 "STR:Finish"
  nsDialogs::Show
FunctionEnd

Function MeetilyFinishPageLeave
  ${NSD_GetState} $MeetilyFinishDesktop $R0
  ${If} $R0 == ${BST_CHECKED}
    Call CreateOrUpdateDesktopShortcut
  ${EndIf}
  ${NSD_GetState} $MeetilyFinishLaunch $R0
  ${If} $R0 == ${BST_CHECKED}
    Call RunMainBinary
  ${EndIf}
FunctionEnd

; Define registry key to store installer language
!define MUI_LANGDLL_REGISTRY_ROOT "HKCU"
!define MUI_LANGDLL_REGISTRY_KEY "${MANUPRODUCTKEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "Installer Language"

; Installer pages, must be ordered as they appear
; 1. Custom welcome page
Page custom MeetilyWelcomePageShow MeetilyWelcomePageLeave

; 2. License Page (if defined)
!if "${LICENSE}" != ""
  !define MUI_PAGE_CUSTOMFUNCTION_PRE SkipIfPassive
  !define MUI_PAGE_CUSTOMFUNCTION_SHOW MeetilyDarkenPage
  !insertmacro MUI_PAGE_LICENSE "${LICENSE}"
!endif

; 3. Install mode (if it is set to `both`)
!if "${INSTALLMODE}" == "both"
  !define MUI_PAGE_CUSTOMFUNCTION_PRE SkipIfPassive
  !insertmacro MULTIUSER_PAGE_INSTALLMODE
!endif

; 4. Custom page to ask user if he wants to reinstall/uninstall
;    only if a previous installation was detected
Var ReinstallPageCheck
Page custom PageReinstall PageLeaveReinstall
Function PageReinstall
  ; Uninstall previous WiX installation if exists.
  ;
  ; A WiX installer stores the installation info in registry
  ; using a UUID and so we have to loop through all keys under
  ; `HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall`
  ; and check if `DisplayName` and `Publisher` keys match ${PRODUCTNAME} and ${MANUFACTURER}
  ;
  ; This has a potential issue that there maybe another installation that matches
  ; our ${PRODUCTNAME} and ${MANUFACTURER} but wasn't installed by our WiX installer,
  ; however, this should be fine since the user will have to confirm the uninstallation
  ; and they can chose to abort it if doesn't make sense.
  StrCpy $0 0
  wix_loop:
    EnumRegKey $1 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall" $0
    StrCmp $1 "" wix_loop_done ; Exit loop if there is no more keys to loop on
    IntOp $0 $0 + 1
    ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$1" "DisplayName"
    ReadRegStr $R1 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$1" "Publisher"
    StrCmp "$R0$R1" "${PRODUCTNAME}${MANUFACTURER}" 0 wix_loop
    ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$1" "UninstallString"
    ${StrCase} $R1 $R0 "L"
    ${StrLoc} $R0 $R1 "msiexec" ">"
    StrCmp $R0 0 0 wix_loop_done
    StrCpy $WixMode 1
    StrCpy $R6 "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\$1"
    Goto compare_version
  wix_loop_done:

  ; Check if there is an existing installation, if not, abort the reinstall page
  ReadRegStr $R0 SHCTX "${UNINSTKEY}" ""
  ReadRegStr $R1 SHCTX "${UNINSTKEY}" "UninstallString"
  ${IfThen} "$R0$R1" == "" ${|} Abort ${|}

  ; Compare this installar version with the existing installation
  ; and modify the messages presented to the user accordingly
  compare_version:
  StrCpy $R4 "$(older)"
  ${If} $WixMode = 1
    ReadRegStr $R0 HKLM "$R6" "DisplayVersion"
  ${Else}
    ReadRegStr $R0 SHCTX "${UNINSTKEY}" "DisplayVersion"
  ${EndIf}
  ${IfThen} $R0 == "" ${|} StrCpy $R4 "$(unknown)" ${|}

  nsis_tauri_utils::SemverCompare "${VERSION}" $R0
  Pop $R0
  ; Reinstalling the same version
  ${If} $R0 = 0
    StrCpy $R1 "$(alreadyInstalledLong)"
    StrCpy $R2 "$(addOrReinstall)"
    StrCpy $R3 "$(uninstallApp)"
    !insertmacro MUI_HEADER_TEXT "$(alreadyInstalled)" "$(chooseMaintenanceOption)"
  ; Upgrading
  ${ElseIf} $R0 = 1
    StrCpy $R1 "$(olderOrUnknownVersionInstalled)"
    StrCpy $R2 "$(uninstallBeforeInstalling)"
    StrCpy $R3 "$(dontUninstall)"
    !insertmacro MUI_HEADER_TEXT "$(alreadyInstalled)" "$(choowHowToInstall)"
  ; Downgrading
  ${ElseIf} $R0 = -1
    StrCpy $R1 "$(newerVersionInstalled)"
    StrCpy $R2 "$(uninstallBeforeInstalling)"
    !if "${ALLOWDOWNGRADES}" == "true"
      StrCpy $R3 "$(dontUninstall)"
    !else
      StrCpy $R3 "$(dontUninstallDowngrade)"
    !endif
    !insertmacro MUI_HEADER_TEXT "$(alreadyInstalled)" "$(choowHowToInstall)"
  ${Else}
    Abort
  ${EndIf}

  ; Skip showing the page if passive
  ;
  ; Note that we don't call this earlier at the beginning
  ; of this function because we need to populate some variables
  ; related to current installed version if detected and whether
  ; we are downgrading or not.
  ${If} $PassiveMode = 1
    Call PageLeaveReinstall
  ${Else}
    nsDialogs::Create 1018
    Pop $R4
    ${IfThen} $(^RTL) = 1 ${|} nsDialogs::SetRTL $(^RTL) ${|}

    ${NSD_CreateLabel} 0 0 100% 24u $R1
    Pop $R1

    ${NSD_CreateRadioButton} 30u 50u -30u 8u $R2
    Pop $R2
    ${NSD_OnClick} $R2 PageReinstallUpdateSelection

    ${NSD_CreateRadioButton} 30u 70u -30u 8u $R3
    Pop $R3
    ; Disable this radio button if downgrading and downgrades are disabled
    !if "${ALLOWDOWNGRADES}" == "false"
      ${IfThen} $R0 = -1 ${|} EnableWindow $R3 0 ${|}
    !endif
    ${NSD_OnClick} $R3 PageReinstallUpdateSelection

    ; Check the first radio button if this the first time
    ; we enter this page or if the second button wasn't
    ; selected the last time we were on this page
    ${If} $ReinstallPageCheck <> 2
      SendMessage $R2 ${BM_SETCHECK} ${BST_CHECKED} 0
    ${Else}
      SendMessage $R3 ${BM_SETCHECK} ${BST_CHECKED} 0
    ${EndIf}

    ${NSD_SetFocus} $R2
    nsDialogs::Show
  ${EndIf}
FunctionEnd
Function PageReinstallUpdateSelection
  ${NSD_GetState} $R2 $R1
  ${If} $R1 == ${BST_CHECKED}
    StrCpy $ReinstallPageCheck 1
  ${Else}
    StrCpy $ReinstallPageCheck 2
  ${EndIf}
FunctionEnd
Function PageLeaveReinstall
  ${NSD_GetState} $R2 $R1

  ; If migrating from Wix, always uninstall
  ${If} $WixMode = 1
    Goto reinst_uninstall
  ${EndIf}

  ; In update mode, always proceeds without uninstalling
  ${If} $UpdateMode = 1
    Goto reinst_done
  ${EndIf}

  ; $R0 holds whether same(0)/upgrading(1)/downgrading(-1) version
  ; $R1 holds the radio buttons state:
  ;   1 => first choice was selected
  ;   0 => second choice was selected
  ${If} $R0 = 0 ; Same version, proceed
    ${If} $R1 = 1              ; User chose to add/reinstall
      Goto reinst_done
    ${Else}                    ; User chose to uninstall
      Goto reinst_uninstall
    ${EndIf}
  ${ElseIf} $R0 = 1 ; Upgrading
    ${If} $R1 = 1              ; User chose to uninstall
      Goto reinst_uninstall
    ${Else}
      Goto reinst_done         ; User chose NOT to uninstall
    ${EndIf}
  ${ElseIf} $R0 = -1 ; Downgrading
    ${If} $R1 = 1              ; User chose to uninstall
      Goto reinst_uninstall
    ${Else}
      Goto reinst_done         ; User chose NOT to uninstall
    ${EndIf}
  ${EndIf}

  reinst_uninstall:
    HideWindow
    ClearErrors

    ${If} $WixMode = 1
      ReadRegStr $R1 HKLM "$R6" "UninstallString"
      ExecWait '$R1' $0
    ${Else}
      ReadRegStr $4 SHCTX "${MANUPRODUCTKEY}" ""
      ReadRegStr $R1 SHCTX "${UNINSTKEY}" "UninstallString"
      ${IfThen} $UpdateMode = 1 ${|} StrCpy $R1 "$R1 /UPDATE" ${|} ; append /UPDATE
      ${IfThen} $PassiveMode = 1 ${|} StrCpy $R1 "$R1 /P" ${|} ; append /P
      StrCpy $R1 "$R1 _?=$4" ; append uninstall directory
      ExecWait '$R1' $0
    ${EndIf}

    BringToFront

    ${IfThen} ${Errors} ${|} StrCpy $0 2 ${|} ; ExecWait failed, set fake exit code

    ${If} $0 <> 0
    ${OrIf} ${FileExists} "$INSTDIR\${MAINBINARYNAME}.exe"
      ; User cancelled wix uninstaller? return to select un/reinstall page
      ${If} $WixMode = 1
      ${AndIf} $0 = 1602
        Abort
      ${EndIf}

      ; User cancelled NSIS uninstaller? return to select un/reinstall page
      ${If} $0 = 1
        Abort
      ${EndIf}

      ; Other errors? show generic error message and return to select un/reinstall page
      MessageBox MB_ICONEXCLAMATION "$(unableToUninstall)"
      Abort
    ${EndIf}
  reinst_done:
FunctionEnd

; 5. Custom install-location page (no stock Wizard97 directory chrome)
Page custom MeetilyDirPageShow MeetilyDirPageLeave

; 6. Start menu shortcut page - skipped; shortcuts still created on finish
Var AppStartMenuFolder
!define MUI_PAGE_CUSTOMFUNCTION_PRE Skip
!if "${STARTMENUFOLDER}" != ""
  !define MUI_STARTMENUPAGE_DEFAULTFOLDER "${STARTMENUFOLDER}"
!endif
!insertmacro MUI_PAGE_STARTMENU Application $AppStartMenuFolder

; 7. Installation page (details + teal progress)
!define MUI_PAGE_CUSTOMFUNCTION_SHOW MeetilyDarkenInstFiles
!define MUI_PAGE_HEADER_TEXT "Installing Talkkeeper"
!define MUI_PAGE_HEADER_SUBTEXT "Copying files and setting up local runtimes…"
!insertmacro MUI_PAGE_INSTFILES

; 8. Custom completion page
Page custom MeetilyFinishPageShow MeetilyFinishPageLeave

Function RunMainBinary
  nsis_tauri_utils::RunAsUser "$INSTDIR\${MAINBINARYNAME}.exe" ""
FunctionEnd

; Uninstaller Pages
; 1. Confirm uninstall page
Var DeleteAppDataCheckbox
Var DeleteAppDataCheckboxState
!define /ifndef WS_EX_LAYOUTRTL         0x00400000
!define MUI_PAGE_CUSTOMFUNCTION_SHOW un.ConfirmShow
Function un.ConfirmShow ; Add add a `Delete app data` check box
  ; $1 inner dialog HWND
  ; $2 window DPI
  ; $3 style
  ; $4 x
  ; $5 y
  ; $6 width
  ; $7 height
  FindWindow $1 "#32770" "" $HWNDPARENT ; Find inner dialog
  System::Call "user32::GetDpiForWindow(p r1) i .r2"
  ${If} $(^RTL) = 1
    StrCpy $3 "${__NSD_CheckBox_EXSTYLE} | ${WS_EX_LAYOUTRTL}"
    IntOp $4 50 * $2
  ${Else}
    StrCpy $3 "${__NSD_CheckBox_EXSTYLE}"
    IntOp $4 0 * $2
  ${EndIf}
  IntOp $5 100 * $2
  IntOp $6 400 * $2
  IntOp $7 25 * $2
  IntOp $4 $4 / 96
  IntOp $5 $5 / 96
  IntOp $6 $6 / 96
  IntOp $7 $7 / 96
  System::Call 'user32::CreateWindowEx(i r3, w "${__NSD_CheckBox_CLASS}", w "$(deleteAppData)", i ${__NSD_CheckBox_STYLE}, i r4, i r5, i r6, i r7, p r1, i0, i0, i0) i .s'
  Pop $DeleteAppDataCheckbox
  SendMessage $HWNDPARENT ${WM_GETFONT} 0 0 $1
  SendMessage $DeleteAppDataCheckbox ${WM_SETFONT} $1 1
FunctionEnd
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE un.ConfirmLeave
Function un.ConfirmLeave
  SendMessage $DeleteAppDataCheckbox ${BM_GETCHECK} 0 0 $DeleteAppDataCheckboxState
FunctionEnd
!define MUI_PAGE_CUSTOMFUNCTION_PRE un.SkipIfPassive
!insertmacro MUI_UNPAGE_CONFIRM

; 2. Uninstalling Page
!insertmacro MUI_UNPAGE_INSTFILES

;Languages
{{#each languages}}
!insertmacro MUI_LANGUAGE "{{this}}"
{{/each}}
!insertmacro MUI_RESERVEFILE_LANGDLL
{{#each language_files}}
  !include "{{this}}"
{{/each}}

Function .onInit
  StrCpy $MeetilyProgressToken ""
  ${GetOptions} $CMDLINE "/PROGRESSTOKEN=" $MeetilyProgressToken
  ${GetOptions} $CMDLINE "/P" $PassiveMode
  ${IfNot} ${Errors}
    StrCpy $PassiveMode 1
  ${EndIf}

  ${GetOptions} $CMDLINE "/NS" $NoShortcutMode
  ${IfNot} ${Errors}
    StrCpy $NoShortcutMode 1
  ${EndIf}

  ${GetOptions} $CMDLINE "/UPDATE" $UpdateMode
  ${IfNot} ${Errors}
    StrCpy $UpdateMode 1
  ${EndIf}

  !if "${DISPLAYLANGUAGESELECTOR}" == "true"
    !insertmacro MUI_LANGDLL_DISPLAY
  !endif

  !insertmacro SetContext

  ${If} $INSTDIR == "${PLACEHOLDER_INSTALL_DIR}"
    ; Set default install location
    !if "${INSTALLMODE}" == "perMachine"
      ${If} ${RunningX64}
        !if "${ARCH}" == "x64"
          StrCpy $INSTDIR "$PROGRAMFILES64\${PRODUCTNAME}"
        !else if "${ARCH}" == "arm64"
          StrCpy $INSTDIR "$PROGRAMFILES64\${PRODUCTNAME}"
        !else
          StrCpy $INSTDIR "$PROGRAMFILES\${PRODUCTNAME}"
        !endif
      ${Else}
        StrCpy $INSTDIR "$PROGRAMFILES\${PRODUCTNAME}"
      ${EndIf}
    !else if "${INSTALLMODE}" == "currentUser"
      StrCpy $INSTDIR "$LOCALAPPDATA\Talkkeeper"
    !endif

    Call RestorePreviousInstallLocation
  ${EndIf}

  ; Start-menu page is skipped; still seed folder for shortcut creation
  !if "${STARTMENUFOLDER}" != ""
    StrCpy $AppStartMenuFolder "${STARTMENUFOLDER}"
  !else
    StrCpy $AppStartMenuFolder "${PRODUCTNAME}"
  !endif

  !if "${INSTALLMODE}" == "both"
    !insertmacro MULTIUSER_INIT
  !endif
FunctionEnd


Section EarlyChecks
  StrCpy $MeetilyReportedProgress 0
  StrCpy $MeetilyDisplayedProgress 0
  !insertmacro MeetilyReportProgress 1
  ; Abort silent installer if downgrades is disabled
  !if "${ALLOWDOWNGRADES}" == "false"
  ${If} ${Silent}
    ; If downgrading
    ${If} $R0 = -1
      System::Call 'kernel32::AttachConsole(i -1)i.r0'
      ${If} $0 <> 0
        System::Call 'kernel32::GetStdHandle(i -11)i.r0'
        System::call 'kernel32::SetConsoleTextAttribute(i r0, i 0x0004)' ; set red color
        FileWrite $0 "$(silentDowngrades)"
      ${EndIf}
      Abort
    ${EndIf}
  ${EndIf}
  !endif

SectionEnd

Section WebView2
  !insertmacro MeetilyReportProgress 3
  ; Check if Webview2 is already installed and skip this section
  ${If} ${RunningX64}
    ReadRegStr $4 HKLM "SOFTWARE\WOW6432Node\Microsoft\EdgeUpdate\Clients\${WEBVIEW2APPGUID}" "pv"
  ${Else}
    ReadRegStr $4 HKLM "SOFTWARE\Microsoft\EdgeUpdate\Clients\${WEBVIEW2APPGUID}" "pv"
  ${EndIf}
  ${If} $4 == ""
    ReadRegStr $4 HKCU "SOFTWARE\Microsoft\EdgeUpdate\Clients\${WEBVIEW2APPGUID}" "pv"
  ${EndIf}

  ${If} $4 == ""
    ; Webview2 installation
    ;
    ; Skip if updating
    ${If} $UpdateMode <> 1
      !if "${INSTALLWEBVIEW2MODE}" == "downloadBootstrapper"
        Delete "$TEMP\MicrosoftEdgeWebview2Setup.exe"
        DetailPrint "$(webview2Downloading)"
        NSISdl::download "https://go.microsoft.com/fwlink/p/?LinkId=2124703" "$TEMP\MicrosoftEdgeWebview2Setup.exe"
        Pop $0
        ${If} $0 == "success"
          DetailPrint "$(webview2DownloadSuccess)"
        ${Else}
          DetailPrint "$(webview2DownloadError)"
          Abort "$(webview2AbortError)"
        ${EndIf}
        StrCpy $6 "$TEMP\MicrosoftEdgeWebview2Setup.exe"
        Goto install_webview2
      !endif

      !if "${INSTALLWEBVIEW2MODE}" == "embedBootstrapper"
        Delete "$TEMP\MicrosoftEdgeWebview2Setup.exe"
        File "/oname=$TEMP\MicrosoftEdgeWebview2Setup.exe" "${WEBVIEW2BOOTSTRAPPERPATH}"
        DetailPrint "$(installingWebview2)"
        StrCpy $6 "$TEMP\MicrosoftEdgeWebview2Setup.exe"
        Goto install_webview2
      !endif

      !if "${INSTALLWEBVIEW2MODE}" == "offlineInstaller"
        Delete "$TEMP\MicrosoftEdgeWebView2RuntimeInstaller.exe"
        File "/oname=$TEMP\MicrosoftEdgeWebView2RuntimeInstaller.exe" "${WEBVIEW2INSTALLERPATH}"
        DetailPrint "$(installingWebview2)"
        StrCpy $6 "$TEMP\MicrosoftEdgeWebView2RuntimeInstaller.exe"
        Goto install_webview2
      !endif

      Goto webview2_done

      install_webview2:
        DetailPrint "$(installingWebview2)"
        ; $6 holds the path to the webview2 installer
        ExecWait "$6 ${WEBVIEW2INSTALLERARGS} /install" $1
        ${If} $1 = 0
          DetailPrint "$(webview2InstallSuccess)"
        ${Else}
          DetailPrint "$(webview2InstallError)"
          Abort "$(webview2AbortError)"
        ${EndIf}
      webview2_done:
    ${EndIf}
  ${Else}
    !if "${MINIMUMWEBVIEW2VERSION}" != ""
      ${VersionCompare} "${MINIMUMWEBVIEW2VERSION}" "$4" $R0
      ${If} $R0 = 1
        update_webview:
          DetailPrint "$(installingWebview2)"
          ${If} ${RunningX64}
            ReadRegStr $R1 HKLM "SOFTWARE\WOW6432Node\Microsoft\EdgeUpdate" "path"
          ${Else}
            ReadRegStr $R1 HKLM "SOFTWARE\Microsoft\EdgeUpdate" "path"
          ${EndIf}
          ${If} $R1 == ""
            ReadRegStr $R1 HKCU "SOFTWARE\Microsoft\EdgeUpdate" "path"
          ${EndIf}
          ${If} $R1 != ""
            ; Chromium updater docs: https://source.chromium.org/chromium/chromium/src/+/main:docs/updater/user_manual.md
            ; Modified from "HKEY_LOCAL_MACHINE\SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\Microsoft EdgeWebView\ModifyPath"
            ExecWait `"$R1" /install appguid=${WEBVIEW2APPGUID}&needsadmin=true` $1
            ${If} $1 = 0
              DetailPrint "$(webview2InstallSuccess)"
            ${Else}
              MessageBox MB_ICONEXCLAMATION|MB_ABORTRETRYIGNORE "$(webview2InstallError)" IDIGNORE ignore IDRETRY update_webview
              Quit
              ignore:
            ${EndIf}
          ${EndIf}
      ${EndIf}
    !endif
  ${EndIf}
  !insertmacro MeetilyReportProgress 10
SectionEnd

Section Install
  !insertmacro MeetilyReportProgress 12
  SetOutPath $INSTDIR

  !ifmacrodef NSIS_HOOK_PREINSTALL
    !insertmacro NSIS_HOOK_PREINSTALL
  !endif

  !insertmacro CheckIfAppIsRunning "${MAINBINARYNAME}.exe" "${PRODUCTNAME}"

  ; Copy main executable
  File "${MAINBINARYSRCPATH}"

  ; Copy resources
  {{#each resources_dirs}}
    CreateDirectory "$INSTDIR\\{{this}}"
  {{/each}}
  {{#each resources}}
    File /a "/oname={{this.[1]}}" "{{no-escape @key}}"
  {{/each}}

  ; Copy external binaries
  {{#each binaries}}
    File /a "/oname={{this}}" "{{no-escape @key}}"
  {{/each}}
  !insertmacro MeetilyReportProgress 72

  ; Create file associations
  {{#each file_associations as |association| ~}}
    {{#each association.ext as |ext| ~}}
       !insertmacro APP_ASSOCIATE "{{ext}}" "{{or association.name ext}}" "{{association-description association.description ext}}" "$INSTDIR\${MAINBINARYNAME}.exe,0" "Open with ${PRODUCTNAME}" "$INSTDIR\${MAINBINARYNAME}.exe $\"%1$\""
    {{/each}}
  {{/each}}

  ; Register deep links
  {{#each deep_link_protocols as |protocol| ~}}
    WriteRegStr SHCTX "Software\Classes\\{{protocol}}" "URL Protocol" ""
    WriteRegStr SHCTX "Software\Classes\\{{protocol}}" "" "URL:${BUNDLEID} protocol"
    WriteRegStr SHCTX "Software\Classes\\{{protocol}}\DefaultIcon" "" "$\"$INSTDIR\${MAINBINARYNAME}.exe$\",0"
    WriteRegStr SHCTX "Software\Classes\\{{protocol}}\shell\open\command" "" "$\"$INSTDIR\${MAINBINARYNAME}.exe$\" $\"%1$\""
  {{/each}}

  ; Create uninstaller
  WriteUninstaller "$INSTDIR\uninstall.exe"

  ; Save $INSTDIR in registry for future installations
  WriteRegStr SHCTX "${MANUPRODUCTKEY}" "" $INSTDIR

  !if "${INSTALLMODE}" == "both"
    ; Save install mode to be selected by default for the next installation such as updating
    ; or when uninstalling
    WriteRegStr SHCTX "${UNINSTKEY}" $MultiUser.InstallMode 1
  !endif

  ; Remove old main binary if it doesn't match new main binary name
  ReadRegStr $OldMainBinaryName SHCTX "${UNINSTKEY}" "MainBinaryName"
  ${If} $OldMainBinaryName != ""
  ${AndIf} $OldMainBinaryName != "${MAINBINARYNAME}.exe"
    Delete "$INSTDIR\$OldMainBinaryName"
  ${EndIf}

  ; Save current MAINBINARYNAME for future updates
  WriteRegStr SHCTX "${UNINSTKEY}" "MainBinaryName" "${MAINBINARYNAME}.exe"

  ; Registry information for add/remove programs
  WriteRegStr SHCTX "${UNINSTKEY}" "DisplayName" "${PRODUCTNAME}"
  WriteRegStr SHCTX "${UNINSTKEY}" "DisplayIcon" "$\"$INSTDIR\${MAINBINARYNAME}.exe$\""
  WriteRegStr SHCTX "${UNINSTKEY}" "DisplayVersion" "${VERSION}"
  WriteRegStr SHCTX "${UNINSTKEY}" "Publisher" "${MANUFACTURER}"
  WriteRegStr SHCTX "${UNINSTKEY}" "InstallLocation" "$\"$INSTDIR$\""
  WriteRegStr SHCTX "${UNINSTKEY}" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
  WriteRegDWORD SHCTX "${UNINSTKEY}" "NoModify" "1"
  WriteRegDWORD SHCTX "${UNINSTKEY}" "NoRepair" "1"

  ${GetSize} "$INSTDIR" "/M=uninstall.exe /S=0K /G=0" $0 $1 $2
  IntOp $0 $0 + ${ESTIMATEDSIZE}
  IntFmt $0 "0x%08X" $0
  WriteRegDWORD SHCTX "${UNINSTKEY}" "EstimatedSize" "$0"

  !if "${HOMEPAGE}" != ""
    WriteRegStr SHCTX "${UNINSTKEY}" "URLInfoAbout" "${HOMEPAGE}"
    WriteRegStr SHCTX "${UNINSTKEY}" "URLUpdateInfo" "${HOMEPAGE}"
    WriteRegStr SHCTX "${UNINSTKEY}" "HelpLink" "${HOMEPAGE}"
  !endif

  ; DisplayVersion is rewritten above on every update. Notify Settings so an
  ; Installed Apps page that was already open does not keep the previous
  ; version from its process-local cache.
  Push $9
  System::Call 'user32::SendMessageTimeoutW(p 0xffff, i 0x001A, p 0, w "Software\\Microsoft\\Windows\\CurrentVersion\\Uninstall", i 0x0002, i 2000, *p .r9)'
  Pop $9

  ; Create start menu shortcut
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    Call CreateOrUpdateStartMenuShortcut
  !insertmacro MUI_STARTMENU_WRITE_END

  ; Create desktop shortcut for silent and passive installers
  ; because finish page will be skipped
  ${If} $PassiveMode = 1
  ${OrIf} ${Silent}
  ${OrIf} $UpdateMode = 1
    Call CreateOrUpdateDesktopShortcut
  ${EndIf}

  !insertmacro MeetilyReportProgress 78

  !ifmacrodef NSIS_HOOK_POSTINSTALL
    !insertmacro NSIS_HOOK_POSTINSTALL
  !endif

  ; The direct NSIS engine has no usable navigation while files are copying.
  ; Reveal Next only after every install and runtime step has completed.
  ${If} $PassiveMode != 1
  ${AndIfNot} ${Silent}
    GetDlgItem $R1 $HWNDPARENT 1
    ShowWindow $R1 ${SW_SHOW}
  ${EndIf}
  !insertmacro MeetilyReportProgress 100
  StrCpy $MeetilyDisplayedProgress 100
  Call MeetilyRefreshInstallProgress
  nsDialogs::KillTimer MeetilyRefreshInstallProgress

  ; Auto close this page for passive mode. In-app updates are visible even in
  ; passive mode, so leave the completed state onscreen briefly instead of
  ; jumping from the last extraction operation straight back into the app.
  ${If} $PassiveMode = 1
    ${If} $UpdateMode = 1
      Sleep 450
    ${EndIf}
    SetAutoClose true
  ${EndIf}
SectionEnd

Function .onInstSuccess
  nsDialogs::KillTimer MeetilyRefreshInstallProgress

  ; All shortcut choices are final now, including the interactive finish-page
  ; desktop option. Refresh Explorer/Search without resetting the icon cache.
  System::Call 'shell32::SHChangeNotify(i 0x08000000, i 0x0000, p 0, p 0)'

  ; Check for `/R` flag only in silent and passive installers because
  ; GUI installer has a toggle for the user to (re)start the app
  ${If} $PassiveMode = 1
  ${OrIf} ${Silent}
    ${GetOptions} $CMDLINE "/R" $R0
    ${IfNot} ${Errors}
      ${GetOptions} $CMDLINE "/ARGS" $R0
      nsis_tauri_utils::RunAsUser "$INSTDIR\${MAINBINARYNAME}.exe" "$R0"
    ${EndIf}
  ${EndIf}
FunctionEnd

Function .onInstFailed
  nsDialogs::KillTimer MeetilyRefreshInstallProgress
FunctionEnd

Function un.onInit
  !insertmacro SetContext

  !if "${INSTALLMODE}" == "both"
    !insertmacro MULTIUSER_UNINIT
  !endif

  !insertmacro MUI_UNGETLANGUAGE

  ${GetOptions} $CMDLINE "/P" $PassiveMode
  ${IfNot} ${Errors}
    StrCpy $PassiveMode 1
  ${EndIf}

  ${GetOptions} $CMDLINE "/UPDATE" $UpdateMode
  ${IfNot} ${Errors}
    StrCpy $UpdateMode 1
  ${EndIf}
FunctionEnd

Section Uninstall

  !ifmacrodef NSIS_HOOK_PREUNINSTALL
    !insertmacro NSIS_HOOK_PREUNINSTALL
  !endif

  !insertmacro CheckIfAppIsRunning "${MAINBINARYNAME}.exe" "${PRODUCTNAME}"

  ; Delete the app directory and its content from disk
  ; Copy main executable
  Delete "$INSTDIR\${MAINBINARYNAME}.exe"
  Delete "$INSTDIR\DirectML.dll"
  Delete "$INSTDIR\cudart64_13.dll"
  Delete "$INSTDIR\cublas64_13.dll"
  Delete "$INSTDIR\cublasLt64_13.dll"

  ; Delete resources
  {{#each resources}}
    Delete "$INSTDIR\\{{this.[1]}}"
  {{/each}}

  ; Delete external binaries
  {{#each binaries}}
    Delete "$INSTDIR\\{{this}}"
  {{/each}}

  ; Delete app associations
  {{#each file_associations as |association| ~}}
    {{#each association.ext as |ext| ~}}
      !insertmacro APP_UNASSOCIATE "{{ext}}" "{{or association.name ext}}"
    {{/each}}
  {{/each}}

  ; Delete deep links
  {{#each deep_link_protocols as |protocol| ~}}
    ReadRegStr $R7 SHCTX "Software\Classes\\{{protocol}}\shell\open\command" ""
    ${If} $R7 == "$\"$INSTDIR\${MAINBINARYNAME}.exe$\" $\"%1$\""
      DeleteRegKey SHCTX "Software\Classes\\{{protocol}}"
    ${EndIf}
  {{/each}}


  ; Delete uninstaller
  Delete "$INSTDIR\uninstall.exe"

  {{#each resources_ancestors}}
  RMDir /REBOOTOK "$INSTDIR\\{{this}}"
  {{/each}}
  RMDir "$INSTDIR"

  ; Remove shortcuts if not updating
  ${If} $UpdateMode <> 1
    !insertmacro DeleteAppUserModelId

    ; Remove start menu shortcut
    !insertmacro MUI_STARTMENU_GETFOLDER Application $AppStartMenuFolder
    !insertmacro IsShortcutTarget "$SMPROGRAMS\$AppStartMenuFolder\${PRODUCTNAME}.lnk" "$INSTDIR\${MAINBINARYNAME}.exe"
    Pop $0
    ${If} $0 = 1
      !insertmacro UnpinShortcut "$SMPROGRAMS\$AppStartMenuFolder\${PRODUCTNAME}.lnk"
      Delete "$SMPROGRAMS\$AppStartMenuFolder\${PRODUCTNAME}.lnk"
      RMDir "$SMPROGRAMS\$AppStartMenuFolder"
    ${EndIf}
    !insertmacro IsShortcutTarget "$SMPROGRAMS\${PRODUCTNAME}.lnk" "$INSTDIR\${MAINBINARYNAME}.exe"
    Pop $0
    ${If} $0 = 1
      !insertmacro UnpinShortcut "$SMPROGRAMS\${PRODUCTNAME}.lnk"
      Delete "$SMPROGRAMS\${PRODUCTNAME}.lnk"
    ${EndIf}

    ; Remove desktop shortcuts
    !insertmacro IsShortcutTarget "$DESKTOP\${PRODUCTNAME}.lnk" "$INSTDIR\${MAINBINARYNAME}.exe"
    Pop $0
    ${If} $0 = 1
      !insertmacro UnpinShortcut "$DESKTOP\${PRODUCTNAME}.lnk"
      Delete "$DESKTOP\${PRODUCTNAME}.lnk"
    ${EndIf}
  ${EndIf}

  ; Remove registry information for add/remove programs
  !if "${INSTALLMODE}" == "both"
    DeleteRegKey SHCTX "${UNINSTKEY}"
  !else if "${INSTALLMODE}" == "perMachine"
    DeleteRegKey HKLM "${UNINSTKEY}"
  !else
    DeleteRegKey HKCU "${UNINSTKEY}"
  !endif

  ; Removes the Autostart entry for ${PRODUCTNAME} from the HKCU Run key if it exists.
  ; This ensures the program does not launch automatically after uninstallation if it exists.
  ; If it doesn't exist, it does nothing.
  ; We do this when not updating (to preserve the registry value on updates)
  ${If} $UpdateMode <> 1
    DeleteRegValue HKCU "Software\Microsoft\Windows\CurrentVersion\Run" "${PRODUCTNAME}"
  ${EndIf}

  ; Delete app data if the checkbox is selected
  ; and if not updating
  ${If} $DeleteAppDataCheckboxState = 1
  ${AndIf} $UpdateMode <> 1
    ; Clear the install location $INSTDIR from registry
    DeleteRegKey SHCTX "${MANUPRODUCTKEY}"
    DeleteRegKey /ifempty SHCTX "${MANUKEY}"

    ; Clear the install language from registry
    DeleteRegValue HKCU "${MANUPRODUCTKEY}" "Installer Language"
    DeleteRegKey /ifempty HKCU "${MANUPRODUCTKEY}"
    DeleteRegKey /ifempty HKCU "${MANUKEY}"

    SetShellVarContext current
    RmDir /r "$APPDATA\${BUNDLEID}"
    RmDir /r "$LOCALAPPDATA\${BUNDLEID}"
    RmDir /r "$INSTDIR\data"
    RmDir "$INSTDIR"
  ${EndIf}

  !ifmacrodef NSIS_HOOK_POSTUNINSTALL
    !insertmacro NSIS_HOOK_POSTUNINSTALL
  !endif

  ; Auto close if passive mode or updating
  ${If} $PassiveMode = 1
  ${OrIf} $UpdateMode = 1
    SetAutoClose true
  ${EndIf}
SectionEnd

Function RestorePreviousInstallLocation
  ReadRegStr $4 SHCTX "${MANUPRODUCTKEY}" ""
  StrCmp $4 "" +2 0
    StrCpy $INSTDIR $4
FunctionEnd

Function Skip
  Abort
FunctionEnd

Function SkipIfPassive
  ${IfThen} $PassiveMode = 1  ${|} Abort ${|}
FunctionEnd
Function un.SkipIfPassive
  ${IfThen} $PassiveMode = 1  ${|} Abort ${|}
FunctionEnd

Function CreateOrUpdateStartMenuShortcut
  ; Updates deliberately recreate the shortcut. CreateShortcut records an
  ; explicit executable icon/index, while SetLnkAppUserModelId uses BUNDLEID;
  ; together these keep Search and taskbar identity stable across replacement.
  ${If} $WixMode = 0
    ${If} $NoShortcutMode = 1
      Return
    ${EndIf}
  ${EndIf}

  !if "${STARTMENUFOLDER}" != ""
    CreateDirectory "$SMPROGRAMS\$AppStartMenuFolder"
    CreateShortcut "$SMPROGRAMS\$AppStartMenuFolder\${PRODUCTNAME}.lnk" "$INSTDIR\${MAINBINARYNAME}.exe" "" "$INSTDIR\${MAINBINARYNAME}.exe" 0 SW_SHOWNORMAL "" "${PRODUCTNAME}"
    !insertmacro SetLnkAppUserModelId "$SMPROGRAMS\$AppStartMenuFolder\${PRODUCTNAME}.lnk"
  !else
    CreateShortcut "$SMPROGRAMS\${PRODUCTNAME}.lnk" "$INSTDIR\${MAINBINARYNAME}.exe" "" "$INSTDIR\${MAINBINARYNAME}.exe" 0 SW_SHOWNORMAL "" "${PRODUCTNAME}"
    !insertmacro SetLnkAppUserModelId "$SMPROGRAMS\${PRODUCTNAME}.lnk"
  !endif
FunctionEnd

Function CreateOrUpdateDesktopShortcut
  ${If} $WixMode = 0
    ${If} $NoShortcutMode = 1
      Return
    ${EndIf}
  ${EndIf}

  ; An update refreshes an existing desktop shortcut but does not create one
  ; for users who left the setup option unchecked.
  ${If} $UpdateMode = 1
  ${AndIfNot} ${FileExists} "$DESKTOP\${PRODUCTNAME}.lnk"
    Return
  ${EndIf}

  CreateShortcut "$DESKTOP\${PRODUCTNAME}.lnk" "$INSTDIR\${MAINBINARYNAME}.exe" "" "$INSTDIR\${MAINBINARYNAME}.exe" 0 SW_SHOWNORMAL "" "${PRODUCTNAME}"
  !insertmacro SetLnkAppUserModelId "$DESKTOP\${PRODUCTNAME}.lnk"
FunctionEnd
